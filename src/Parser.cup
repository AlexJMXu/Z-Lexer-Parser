import java_cup.runtime.*;
import java.util.ArrayList;

/*
TODO Do we have to check for undefined variables?
TODO Do we have to check if types are compatible?
*/

scan with {: return lexer.next_token(); :};

parser code {:

  private boolean debug = false;
  public boolean  debug()         {return debug;}
  public void     debug(boolean b){debug = b;}

  static class Node {
    public ArrayList<Node> children;
    private static int globalCounter = 0;

    public int counter;
    private Object value;

    public Node(Object value){
      Node.globalCounter += 1;
      this.counter = Node.globalCounter;
      this.value = value;
      this.children = new ArrayList<Node>();
    }

    public void addChild(Node n){ children.add(n); }

    public String toString(){
      String ret = "";
      ret += String.format("%d [label=\"%s\"];\n",
                            counter, value.toString());
      for(Node n: children){
        ret += String.format("%d -> %d;\n", this.counter, n.counter);
        ret += n.toString();
      }
      return ret;
    }
  }

  private Lexer lexer;

  public Parser(Lexer lex) {
    super(lex);
    lexer = lex;
  }

:};

terminal INTEGER, BOOLEAN, CHARACTER, RATIONAL, FLOAT, DICTIONARY, SEQUENCE, VOID, TOP;
terminal TRUE, FALSE;
terminal WHILE, IF, THEN, ELSE, ELSE_IF;
terminal IN, ALIAS, TYPEDEF, FINISH, DO, PRINT, READ, FORALL, FUNCTION_DEFINITION, OD, RETURN;
terminal IDENTIFIER, STRING_LITERAL, CHAR_LITERAL, NUMBER_LITERAL;
terminal EQ, EQEQ, PLUS, MINUS, MULT, SLASH, CARET, UNDERSCORE, COMMA, COLON, SEMI_COLON, PERIOD, MAIN, LEN;
terminal NEGATION, AND, OR;
terminal L_BRACKET, R_BRACKET, L_SQUARE_BRACKET, R_SQUARE_BRACKET, L_CURLY_BRACKET, R_CURLY_BRACKET, L_PAREN, R_PAREN;

nonterminal statements, statement, program, td;
nonterminal main_function, global_space;
nonterminal datatype, primitivedatatype, aggregatedatatype;
nonterminal number, full_exp, dictlistobj, assignment, return_type, function, parameter_list, conditional, elseifs;
nonterminal declist, declist2, declistobj, seqlistwithoutid, seqlist, seqlist2, seqlistobj, dictlist, dictlistwithoutid, dictlist2;
nonterminal control_flow, forall;
nonterminal conditions, condition;
nonterminal alias, print;
nonterminal exp, unary_operator, binary_operator, integer_number, float_number, rational_number;

precedence left PLUS, MINUS;
precedence left SLASH, MULT;

/* main has to be defined */
program ::= main_function
          | global_space main_function
          | main_function global_space
          | global_space main_function global_space
          ; 

/* anything outside the main function can only be an assignment or function */
global_space ::= global_space full_exp
               | full_exp
               ;

statements ::= statements:ss statement:s {:
                  Node n = (Node)ss;
                  n.addChild((Node)s);
                  RESULT = n;
               :}
             | /* empty */ {:
                  RESULT = new Node("Statements");
               :}
             ;

statement ::= full_exp:s {:
                RESULT = (Node)s;
              :}
            | control_flow
            | print SEMI_COLON
            ;

full_exp ::= assignment:a {: RESULT = new Node(a); :} SEMI_COLON
           | td:t {: RESULT = new Node(t); :} SEMI_COLON
           | function:f {: RESULT = new Node(f); :} SEMI_COLON
           | alias SEMI_COLON
           ;                         

main_function ::= MAIN L_CURLY_BRACKET statements:ss R_CURLY_BRACKET;

function ::= FUNCTION_DEFINITION:f return_type IDENTIFIER:i L_PAREN:lp parameter_list R_PAREN:rp L_CURLY_BRACKET:lc statements:ss R_CURLY_BRACKET:rc
           ;

/* parameter list for function */
parameter_list ::= declist
                 ;

number ::= rational_number
        |  float_number
        |  integer_number
        ;

exp ::= number
      | CHAR_LITERAL
      | IDENTIFIER
      | exp binary_operator exp
      | L_PAREN exp R_PAREN
      | unary_operator exp
      | IDENTIFIER L_PAREN parameter_list R_PAREN //Allow functions inside expressions  e.g. foo(int a, char c)
      ;

unary_operator ::= NEGATION {: RESULT = new Node("!"); :}; 

binary_operator ::= PLUS  {: RESULT = new Node("+"); :}
           | MINUS {: RESULT = new Node("-"); :}
           | MULT  {: RESULT = new Node("*"); :}
           | SLASH  {: RESULT = new Node("/"); :}
           ;      

integer_number ::= NUMBER_LITERAL:num
        |  MINUS NUMBER_LITERAL:num
        ;

float_number ::= NUMBER_LITERAL:num1 PERIOD NUMBER_LITERAL:num2
              |  MINUS NUMBER_LITERAL:nu1 PERIOD NUMBER_LITERAL:num2
              ;

rational_number ::= integer_number SLASH integer_number
                 |  integer_number UNDERSCORE integer_number SLASH integer_number
                 ;

/* TODO Add tdef types to datatype */

datatype ::= primitivedatatype:p {: RESULT = new Node(p); :}
           | aggregatedatatype:a {: RESULT = new Node(a); :}
           ;

primitivedatatype ::= INTEGER:i {: RESULT = new Node("int"); :}
                    | BOOLEAN:b {: RESULT = new Node("bool"); :}
                    | RATIONAL:r {: RESULT = new Node("rat"); :}
                    | CHARACTER:c {: RESULT = new Node("char"); :}
                    | FLOAT:f {: RESULT = new Node("float"); :}
                    | TOP:t {: RESULT = new Node("top"); :} 
                    ;

/* valid return types for functions */
return_type ::= datatype
              | VOID {: RESULT = new Node("void"); :}
              ;

aggregatedatatype ::= DICTIONARY L_BRACKET primitivedatatype:p COMMA primitivedatatype:p2 R_BRACKET
                    | SEQUENCE L_BRACKET primitivedatatype:p R_BRACKET
                    ;                 

assignment ::= IDENTIFIER IDENTIFIER:id EQ exp
            | primitivedatatype IDENTIFIER:id EQ exp
            | SEQUENCE L_BRACKET primitivedatatype R_BRACKET IDENTIFIER EQ L_SQUARE_BRACKET seqlist:sl R_SQUARE_BRACKET
            | SEQUENCE L_BRACKET IDENTIFIER R_BRACKET IDENTIFIER EQ L_SQUARE_BRACKET seqlist:sl R_SQUARE_BRACKET
            | SEQUENCE L_BRACKET primitivedatatype R_BRACKET IDENTIFIER EQ STRING_LITERAL
            | SEQUENCE L_BRACKET IDENTIFIER R_BRACKET IDENTIFIER EQ STRING_LITERAL
            | DICTIONARY L_BRACKET primitivedatatype COMMA primitivedatatype R_BRACKET IDENTIFIER EQ L_CURLY_BRACKET dictlist:dl R_CURLY_BRACKET
            | DICTIONARY L_BRACKET IDENTIFIER COMMA IDENTIFIER R_BRACKET IDENTIFIER EQ L_CURLY_BRACKET dictlist:dl R_CURLY_BRACKET
            | DICTIONARY L_BRACKET primitivedatatype COMMA IDENTIFIER R_BRACKET IDENTIFIER EQ L_CURLY_BRACKET dictlist:dl R_CURLY_BRACKET
            | DICTIONARY L_BRACKET IDENTIFIER COMMA primitivedatatype R_BRACKET IDENTIFIER EQ L_CURLY_BRACKET dictlist:dl R_CURLY_BRACKET
            ;                    

conditions ::= condition
            | condition OR conditions
            | condition AND conditions
            ;

condition ::= exp L_BRACKET exp
            | exp R_BRACKET exp
            | exp EQEQ exp
            | exp L_BRACKET EQ exp
            | exp R_BRACKET EQ exp
            | exp unary_operator EQ exp
            | exp
            ;

declist ::= declist2:dl2
          | {: /* empty */ :}     
          ;

declist2 ::= declistobj:dlo COMMA:c declist2:dl {: 
                  Node n = new Node(dl);
                  n.addChild(new Node(dlo));
                  RESULT = n; :}
          | declistobj:dlo {: RESULT = new Node(dlo); :}        
          ;        

declistobj ::= datatype:d IDENTIFIER:i {:
                  RESULT = (Node)d; 
               :}
             ;

seqlist ::= seqlist2:sl2
          | {: /* empty */ :}
          ;

seqlist2 ::= seqlistobj:slo COMMA:c seqlist2:sl {:
                  Node n = new Node(sl);
                  n.addChild(new Node(slo));
                  RESULT = n; :}
           | seqlistobj:slo {: RESULT = new Node(slo); :}
           ;      

seqlistwithoutid ::= L_SQUARE_BRACKET seqlist:sl R_SQUARE_BRACKET;   

seqlistobj ::= exp:e
             | seqlistwithoutid:sl
             ; 

dictlist ::= dictlist2:dl2
           | {: /* empty */ :}
           ;

dictlist2 ::= dictlistobj:dlo COMMA:c dictlist2:dl {:
                  Node n = new Node(dl);
                  n.addChild(new Node(dlo));
                  RESULT = n; :}
            | dictlistobj:dlo {: RESULT = new Node(dlo); :}
            ; 

dictlistobj ::= exp:e1 COLON:c exp:e2 {:
                  Node n = new Node(c);
                  n.addChild(new Node(e1));
                  n.addChild(new Node(e2)); 
                  RESULT = n; :}
              | dictlistwithoutid:dl   
;

dictlistwithoutid ::= L_CURLY_BRACKET dictlist:dl R_CURLY_BRACKET;

td ::= TYPEDEF:t IDENTIFIER:i L_CURLY_BRACKET:l declist:dl R_CURLY_BRACKET:r {: 
                  RESULT = new Node(t);
:};

/* collection of control flow e.g. if and forall */
control_flow ::= forall
               | conditional
               ;

forall ::= FORALL L_PAREN IDENTIFIER IN IDENTIFIER R_PAREN DO statements:ss OD
         ;

conditional ::= IF L_PAREN conditions R_PAREN THEN statements:ss FINISH //TODO Change identifiers to conditions
              | IF L_PAREN conditions R_PAREN THEN statements:ss2 elseifs:eif
;            
              
elseifs ::= ELSE_IF L_PAREN IDENTIFIER:i R_PAREN THEN statements:ss elseifs:e
          | ELSE statements:ss2 FINISH
;

alias ::= ALIAS datatype IDENTIFIER
        | ALIAS IDENTIFIER IDENTIFIER
        ;


print ::= PRINT exp;

